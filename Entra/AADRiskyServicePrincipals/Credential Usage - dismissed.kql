// Objective: Summarizes dismissed risky service principals in Entra ID, joining AADRiskyServicePrincipals, risk events, and sign-ins for full context.
// - Portal: Entra admin center > Identity Protection > Risky Service Principals
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// Set arrays to filter for specific SPNs or risk types; leave empty for all.
let TargetAppIds = dynamic([]);              // e.g., ["00000003-0000-0000-c000-000000000000"]
let TargetRiskTypes = dynamic([]);           // e.g., ["unfamiliarFeatures"]
//
//-----Part 1: Build SP sign-in and risk event join-----
// Only keep SP sign-ins with a risk event type, join to risk events.
let spsignin =
AADServicePrincipalSignInLogs
| where TimeGenerated > ago(30d)
| where (array_length(TargetAppIds) == 0 or AppId in (TargetAppIds))
| extend SigninTime = TimeGenerated
| join kind=inner (
    AADServicePrincipalRiskEvents
    | where Type == 'AADServicePrincipalRiskEvents'
    | where TimeGenerated > ago(30d)
    | where (array_length(TargetRiskTypes) == 0 or RiskEventType in (TargetRiskTypes))
) on AppId;
//
//-----Part 2: Join to latest dismissed/ remediated risky SPs (arg_max)-----
AADRiskyServicePrincipals
| join kind=inner spsignin on AppId
| summarize arg_max(TimeGenerated, *) by AppId
| where RiskState contains 'dismissed'      // Only show dismissed/ remediated. Remove/comment for all states.
//
//-----Part 3: Final output-----
// One row per dismissed SP, showing latest dismissal and risk context.
| distinct TimeGenerated, AppId, ServicePrincipalName, DetectionTimingType, SigninTime, RiskEventType, RiskLevel, Source, RiskDetail, RiskState, ConditionalAccessPolicies
| sort by TimeGenerated desc
| take 50
