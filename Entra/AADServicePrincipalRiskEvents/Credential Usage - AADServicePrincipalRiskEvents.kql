// Objective: Correlates risky Entra ID service principal sign-ins with risk detections using AADServicePrincipalRiskEvents and AADServicePrincipalSignInLogs.
// - Portal: Entra admin center > Identity Protection > Risky service principals & sign-ins
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// Set arrays to filter for specific apps or risk types; leave empty for all.
let TargetApps = dynamic([]);              // e.g., ["MyAppServicePrincipal"]
let TargetRiskTypes = dynamic([]);         // e.g., ["unfamiliarFeatures"]
//
//-----Part 1: Build a SP sign-in table-----
let spsignin =
AADServicePrincipalSignInLogs
| where TimeGenerated > ago(30d)
| where (array_length(TargetApps) == 0 or ServicePrincipalName in (TargetApps))
| extend SigninTime = TimeGenerated;
//
//-----Part 2: Join SP Risk Events to Sign-ins-----
AADServicePrincipalRiskEvents
| where TimeGenerated > ago(30d)
| where (array_length(TargetRiskTypes) == 0 or RiskEventType in (TargetRiskTypes))
| join kind=inner spsignin on AppId
//
//-----Part 3: Calculate detection lag and project key columns-----
| extend RiskTime = TimeGenerated
| extend TimeDelta = abs(SigninTime - RiskTime)
| project TimeGenerated, ServicePrincipalName, DetectionTimingType, SigninTime, RiskTime, TimeDelta, RiskEventType, Source, RiskDetail, RiskState, ConditionalAccessPolicies
//
//-----Part 4: Analyst Filtering-----
// By default, exclude remediated/dismissed risks; comment out to show all.
| where RiskState <> "remediated"
| where RiskState <> "dismissed"
| sort by TimeGenerated desc
| take 50
