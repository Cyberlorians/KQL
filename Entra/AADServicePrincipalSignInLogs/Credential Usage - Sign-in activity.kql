// Objective: Summarizes Entra ID service principal sign-in activity using AADServicePrincipalSignInLogs.
// - Portal: Entra admin center > Monitoring > Service principal sign-in logs
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
let TargetServicePrincipals = dynamic([]);  // e.g., ["TEAMSforRhett"]
let TargetAppIds = dynamic([]);             // e.g., ["45936e86-569d-47fa-9f6b-de1a80576546"]
let TargetResults = dynamic([]);            // e.g., ["0"] for success, "7000222" for failure
//
//-----Part 1: Filter for SP Sign-In Activity-----
AADServicePrincipalSignInLogs
| where TimeGenerated > ago(30d)
| where OperationName == "Sign-in activity"
//
//-----Part 2: Analyst Filtering-----
| where (array_length(TargetServicePrincipals) == 0 or ServicePrincipalName in (TargetServicePrincipals))
| where (array_length(TargetAppIds) == 0 or AppId in (TargetAppIds))
| where (array_length(TargetResults) == 0 or ResultType in (TargetResults))
//
//-----Part 3: Summarize for Dashboard/Export-----
// Include ResultDescription for the success/failure reason.
| summarize SigninCount = count() by TimeGenerated, ServicePrincipalName, AppId, ResultType, ResultSignature, ResultDescription
| sort by TimeGenerated desc
| take 50
