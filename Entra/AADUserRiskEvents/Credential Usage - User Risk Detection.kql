// Objective: Correlates risky user detections to Entra ID sign-ins using AADUserRiskEvents and SigninLogs, with detection lag and targeting.
// - Portal: Entra admin center > Identity Protection > Risky users & sign-ins
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// Set arrays to filter for specific users, apps, or risk event types; leave empty for all.
let TargetUsers = dynamic([]);             // e.g., ["user@domain.com"]
let TargetApps = dynamic([]);              // e.g., ["Microsoft Teams Web Client"]
let TargetRiskTypes = dynamic([]);         // e.g., ["unfamiliarFeatures"]
//
//-----Part 1: Build a sign-in table with risk events-----
// Only keep sign-ins with a risk event type.
let Signin = 
SigninLogs
| where TimeGenerated > ago(30d)
| extend SigninTime = TimeGenerated
| where RiskEventTypes_V2 != "[]";
//
//-----Part 2: Filter for User Risk Events and Join to Sign-Ins-----
AADUserRiskEvents
| where TimeGenerated > ago(90d)
| where OperationName == "User Risk Detection"
| join kind=inner Signin on CorrelationId
//
//-----Part 3: Analyst Filtering-----
// Return only results matching dynamic arrays; leave arrays empty to include all.
| where (array_length(TargetUsers) == 0 or UserPrincipalName in (TargetUsers))
| where (array_length(TargetApps) == 0 or AppDisplayName in (TargetApps))
| where (array_length(TargetRiskTypes) == 0 or RiskEventType in (TargetRiskTypes))
//
//-----Part 4: Calculate Detection Lag and Project Key Fields-----
| extend RiskTime = TimeGenerated
| extend TimeDelta = abs(SigninTime - RiskTime)
| project TimeGenerated, UserPrincipalName, AppDisplayName, DetectionTimingType, SigninTime, RiskTime, TimeDelta, RiskEventType, RiskLevelDuringSignIn, Source, RiskDetail, RiskState, AuthenticationDetails
//
//-----Part 5: Analyst Filtering-----
// Exclude remediated/dismissed risks by default; comment out to show all states.
| where RiskState <> "remediated"
| where RiskState <> "dismissed"
| sort by TimeGenerated desc
| take 50
