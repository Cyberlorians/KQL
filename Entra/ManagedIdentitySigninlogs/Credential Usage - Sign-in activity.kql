// Objective: Summarizes managed identity sign-in activity in Entra ID via AADManagedIdentitySignInLogs.
// - Portal: Entra admin center > Monitoring > Managed identity sign-in logs
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
let TargetManagedIdentities = dynamic([]);   // e.g., ["RCCE-SVR19-1"]
let TargetAppIds = dynamic([]);              // e.g., ["14a9b473-e9ba-43fb-b141-023182cc87d7"]
let TargetResults = dynamic([]);             // e.g., ["0"]
//
//-----Part 1: Filter for Managed Identity Sign-In Activity-----
AADManagedIdentitySignInLogs
| where TimeGenerated > ago(30d)
| where OperationName == "Sign-in activity"
//
//-----Part 2: Analyst Filtering-----
| where (array_length(TargetManagedIdentities) == 0 or ServicePrincipalName in (TargetManagedIdentities))
| where (array_length(TargetAppIds) == 0 or AppId in (TargetAppIds))
| where (array_length(TargetResults) == 0 or ResultType in (TargetResults))
//
//-----Part 3: Summarize for Dashboard/Export-----
// Include ResultDescription for the human-readable outcome; ResourceDisplayName for context.
| summarize SigninCount = count() by TimeGenerated, ServicePrincipalName, AppId, ResourceDisplayName, ResultType, ResultSignature, ResultDescription
| sort by TimeGenerated desc
| take 50
