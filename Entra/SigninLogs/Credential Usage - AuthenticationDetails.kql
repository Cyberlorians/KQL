// Objective: Audits interactive Entra sign-ins by privileged users (SigninLogs), showing which authentication method was used (e.g., FIDO2, OTP, password).
// Useful for verifying credential usage and strong authentication across privileged accounts.
//
// Supports ICAM Pillar: "Track Usage > Credential Usage"
// Category: Privileged Identity & Credential Management (PIM)
//
// This query audits credential usage by expanding `AuthenticationDetails`
// from interactive sign-ins in Entra ID.
//
// Key Characteristics:
// - Tracks the authentication method used per sign-in (e.g., Password, FIDO2, OTP, etc.)
// - Filters to specific privileged users (if provided)
// - Removes non-contributive entries like "Previously satisfied"
// - Useful for verifying strong auth (e.g., phishing-resistant methods) in daily use
// ----- Part 0: Dynamic Targeting (Entra Sign-ins) -----
let TargetUPNs = dynamic([]);  // e.g., ["admin@domain.com", "svc_jdoe@domain.com"]
let ExcludeSyncAccounts = true;         // Set to false to include all accounts
// ----- Part 1: Filter Interactive Sign-ins and Expand Credential Use -----
SigninLogs //Interactive user sign-ins are sign-ins that require the user to supply an authentication factor
| where TimeGenerated > ago(30d)
| mv-expand todynamic(AuthenticationDetails)
| extend
    AuthMethod = tostring(AuthenticationDetails.authenticationMethod),
    IsSyncAccount = UserPrincipalName has "Sync"
| where AuthMethod <> "Previously satisfied"
| where array_length(TargetUPNs) == 0 or UserPrincipalName in (TargetUPNs)
| where ExcludeSyncAccounts == false or IsSyncAccount == false
// ----- Part 2: Return Most Recent Credential Use Per UPN + Method -----
| summarize arg_max(TimeGenerated, Category, AppDisplayName) by UserPrincipalName, AuthMethod
// ----- Part 3: Final Output -----
| project TimeGenerated, UserPrincipalName, AuthMethod, Category, AppDisplayName
| sort by TimeGenerated desc
| take 50
