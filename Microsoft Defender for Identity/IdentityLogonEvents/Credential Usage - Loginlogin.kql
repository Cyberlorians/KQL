// Objective: Summarizes logon activity (success/failure) by account in Microsoft Defender for Identity (IdentityLogonEvents), with cloud and authentication context.
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
let TargetAccounts = dynamic([]);     // e.g., ["svc_account", "user1"]
//
//-----Part 1: Filter for logons by targeted accounts-----
IdentityLogonEvents
| where TimeGenerated > ago(30d)
| where LogonType == "Login:login"
| where (array_length(TargetAccounts) == 0 or AccountName in~ (TargetAccounts))
| extend AF = parse_json(AdditionalFields)
| extend CloudService = tostring(AF['TO.CLOUD_SERVICE']), 
         PassThroughAuth = tostring(AF['Pass-through authentication'])
//
//-----Part 2: Summarize logon activity and outcomes-----
| summarize 
    TotalEvents = count(),
    FailedLogons = countif(ActionType =~ "LogonFailed"),
    SuccessfulLogons = countif(ActionType =~ "LogonSuccess"),
    DistinctCloudServices = make_set(CloudService, 5),
    DistinctIP = make_set(IPAddress, 5),
    LastLogonTime = arg_max(TimeGenerated, AccountUpn, Application, OSPlatform, Location, ISP)
    by AccountName, AccountDomain
| distinct AccountName, AccountDomain, AccountUpn, Application, OSPlatform, Location, ISP, 
          TotalEvents, SuccessfulLogons, FailedLogons, tostring(DistinctCloudServices), tostring(DistinctIP), LastLogonTime
| sort by TotalEvents desc
| take 50
