// Objective: Tracks failed RDP logon attempts (Event ID 4625, LogonType 10) in Windows SecurityEvent logs, enabling targeted filtering by user name and reporting on brute-force or password spray attacks via RDP.
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// To see all activity, leave the list empty: dynamic([])
// To target specific users, add their user names inside the brackets.
let Targeted_User_Names = dynamic([]); // Example: dynamic(["administrator", "xadmin"])
// 
// ----- Part 1: Filter for Relevant 4625 Events (RDP Only) -----
// GPO Path: To capture 4625 events, you must enable the following subcategory:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Logon/Logoff > Audit Logon (Failure)
// Note: This query filters for LogonType == 10 to specifically track failed RDP logons, which is key for detecting brute-force attacks.
SecurityEvent
| where TimeGenerated > ago(14d)
| where EventID == 4625
| where Activity == "4625 - An account failed to log on."
| where AccountType == "User"
//
// ----- Part 2: RDP & Custom Filtering -----
// Focus on failed RDP logons, exclude common service accounts.
| where LogonType == 10
| where not(TargetUserName has_any ('svc', 'MSOL', '$', 'himds'))
//
// ----- Part 3: Flexible Filtering for Targeted Accounts -----
// Apply analyst-driven name filtering if used.
| where array_length(Targeted_User_Names) == 0 or TargetUserName in (Targeted_User_Names)
//
// ----- Part 4: Analysis & Enrichment -----
// Enrichment for failure reason, targeting, and context.
| extend
    LogonTypeName = "RemoteInteractive (RDP)",
    FailureReason = case(
        SubStatus == "0xc000006a", "Bad Password",
        SubStatus == "0xc0000064", "Bad Username",
        SubStatus == "0xc0000234", "Account Locked Out",
        SubStatus == "0xc0000072", "Account Disabled",
        SubStatus == "0xc000006f", "Logon Outside Authorized Hours",
        SubStatus == "0xc0000070", "Unauthorized Workstation",
        "Other Failure"
    ),
    IsTargetedUser = TargetUserName in (Targeted_User_Names),
    FailureContext = iff(IpAddress == "0.0.0.0", "Failure at Network Level Authentication (NLA)", "")
//
// ----- Part 5: Final Output -----
// Unique failed RDP logon events, sorted for review.
| distinct TimeGenerated, IsTargetedUser, TargetUserName, Computer, LogonProcessName, LogonTypeName, FailureReason, IpAddress, FailureContext, Status, SubStatus, Activity
| sort by TimeGenerated desc
| take 50
