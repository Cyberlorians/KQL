// Objective: Tracks 'Run As' activity (Event ID 4648) in Windows SecurityEvent logs, enabling targeted filtering by target user name and highlighting cross-user explicit credential usage by privileged accounts.
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// Define privileged user names for focused analysis. Leave empty to include all accounts.
// This is directed at the TARGET user for cross-user activity.
let Privileged_User_Names = dynamic([]); // Example: dynamic(["administrator", "xadmin"])
// 
// ----- Part 1: Filter for Relevant 4648 Events -----
// GPO Path: To capture 4648 events, you must enable the following subcategory:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Logon/Logoff > Audit Logon (Success)
// Note: This event is critical for tracking 'Run As' activity. We must target by name as the Target SID is not available.
SecurityEvent
| where TimeGenerated > ago(14d)
| where EventID == 4648
| where Activity == "4648 - A logon was attempted using explicit credentials."
| where AccountType == "User"
//
// ----- Part 2: Custom Filtering -----
// Exclude common service and system accounts.
| where not(TargetUserName has_any ('svc', 'MSOL', '$'))
//
// ----- Part 3: Flexible Filtering for Targeted Accounts -----
// Apply analyst-driven user name filtering if used.
| where array_length(Privileged_User_Names) == 0 or TargetUserName in (Privileged_User_Names)
//
// ----- Part 4: Analysis & Enrichment -----
// Flags for privileged logons and cross-user explicit credential usage.
| extend
    IsPrivilegedLogon = TargetUserName in (Privileged_User_Names),
    IsCrossUserActivity = (SubjectUserName != TargetUserName)
//
// ----- Part 5: Final Output -----
// Unique explicit credential usage events, sorted for review.
| distinct TimeGenerated, IsPrivilegedLogon, IsCrossUserActivity, SubjectUserName, TargetUserName, Computer, ProcessName, IpAddress, Activity
| sort by TimeGenerated desc
| take 50
