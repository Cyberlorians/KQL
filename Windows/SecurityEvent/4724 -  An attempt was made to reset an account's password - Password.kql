// Objective: Detects attempts to reset account passwords in Windows via SecurityEvent logs.
//
// ----- Part 0: Define Targeted Users -----
// To see all activity, leave the lists empty: dynamic([])
// To target specific accounts, add their usernames inside the brackets.
//
let TargetUserNames = dynamic([]);        // Example: dynamic(["targetuser1"])
let InitiatedByUserNames = dynamic([]);   // Example: dynamic(["adminuser1"])
//
// GPO Path: To capture 4724 events, you must enable this subcategory:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Account Management > Audit User Account Management (Success and Failure)
// Note: Event 4724 is triggered whenever an attempt is made to reset an account’s password.
//
// ----- Part 1: Filter SecurityEvent for password reset attempts -----
//
SecurityEvent
| where TimeGenerated > ago(14d)
| where EventID == 4724
| where Activity == "4724 - An attempt was made to reset an account's password."
//
// ----- Part 2: Optionally filter by initiator or target usernames as well as by account type -----
//
| where array_length(TargetUserNames) == 0 or TargetUserName in (TargetUserNames)
| where array_length(InitiatedByUserNames) == 0 or SubjectUserName in (InitiatedByUserNames)
| where AccountType <> "User" // filter account to see all or filter by machine
//
// ----- Part 3: Add Targeted Flag -----
//
| extend IsTargetedUser = TargetUserName in (TargetUserNames)
//
// ----- Part 4: Project the fields you care about -----
//
| distinct TimeGenerated, IsTargetedUser, InitiatedBy = SubjectUserName, TargetAccount = TargetUserName, Computer, Activity
//
// ----- Part 5: Sort and limit results -----
//
| sort by TimeGenerated desc
| take 50
