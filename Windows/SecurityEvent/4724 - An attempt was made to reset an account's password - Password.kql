// Objective: Detects password reset attempts on privileged accounts using Windows SecurityEvent (EventID 4724).
// Enables monitoring and alerting for unauthorized or suspicious password reset activity.
//
// ----- Part 0: Optional Targeting -----
//
// ----- Targeting: Populate with accounts to monitor or leave empty for all -----
let PrivUsers = dynamic([]); // e.g., ["admin1", "svc_critical", "priv"]
//
// ----- Part 1: Pull password reset attempts (Event ID 4724) -----
//
SecurityEvent
// GPO Path: To capture 4724 events (password reset attempts), enable:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration >  Audit Policy > Account Management > Audit User Account Management (Success/Failure)
| where TimeGenerated > ago(14d)
| where EventID == 4724
| where Activity == "4724 - An attempt was made to reset an account's password."
//
// ----- Part 2: Optional Security-focused Filtering -----
// Only show events for monitored accounts (if PrivUsers array is used)
//
| where (array_length(PrivUsers) == 0 or TargetUserName in (PrivUsers))
//
// ----- Part 3: Add IsTargetedUser Flag -----
//
| extend IsTargetedUser = TargetUserName in (PrivUsers)
//
// ----- Part 4: Final Output -----
//
| distinct TimeGenerated, IsTargetedUser, Initiator=SubjectUserName, InitiatorDomain=SubjectDomainName,
          TargetUser=TargetUserName, TargetDomain=TargetDomainName, PrivilegeList, SubjectUserSid, TargetSid, Activity
| sort by TimeGenerated desc
| take 50
