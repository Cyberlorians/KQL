// Objective: Detects changes to user accounts (attributes, control values) in Windows via SecurityEvent logs (Event ID 4738).
//
//-----Part 0: Dynamic Targeting-----
let TargetUsers = dynamic([]);        // e.g., ["jdoe"]
let InitiatorUsers = dynamic([]);     // e.g., ["admin"]
//
//-----Part 1: Filter for 4738 Events-----
SecurityEvent
// GPO Path: To capture 4738 events, enable:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Account Management > Audit User Account Management (Success)
// Note: 4738 logs the initiator, target, and key account attributes. No parsing required if fields are top-level.
| where TimeGenerated > ago(30d)
| where EventID == 4738
| where Activity == "4738 - A user account was changed."
//
//-----Part 2: Analyst Filtering-----
| where (array_length(TargetUsers) == 0 or TargetUserName in (TargetUsers))
| where (array_length(InitiatorUsers) == 0 or SubjectUserName in (InitiatorUsers))
//
//-----Part 3: Final Output-----
// Shows: who made the change, which account, and what was changed.
| project TimeGenerated, Activity, ['Initiated By'] = SubjectUserName, InitiatorDomain = SubjectDomainName, TargetUserName, TargetDomain = TargetDomainName, PasswordLastSet, OldUacValue, NewUacValue, UserAccountControl
| sort by TimeGenerated desc
| take 100
