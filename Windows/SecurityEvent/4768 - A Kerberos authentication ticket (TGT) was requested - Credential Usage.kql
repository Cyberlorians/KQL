// Objective: Detects Kerberos TGT (4768) requests using certificate authentication (PKINIT) via Windows SecurityEvent.
// Tracks strong-auth sign-ins (e.g., PIV/CAC/smartcard) by privileged users and endpoints for audit and compliance.
//
// Supports ICAM Pillar: "Manage/Track Changes in Attributes & Credentials"
// Category: Privileged Identity & Credential Management (PIM)
//
// This query tracks 4768 Kerberos TGT requests where a certificate (PKINIT) was used.
// These are strong indicators of privileged sign-in (e.g., HSPD-12 or smartcard logon).
//
// Key Characteristics:
// - Detects cert-based logons to domain controllers
// - Targets specific users or certificate issuers
// - Device (Computer) can now be filtered to narrow down privileged endpoints
//
//-----Part 0: Dynamic Targeting-----
// To target specific users, certificate issuers, or devices, fill in the lists below; leave empty for all.
let TargetUsers = dynamic([]);            // e.g., ["privuser"]
let TargetCertIssuers = dynamic([]);      // e.g., ["Issuing-CA"]
let TargetDevices = dynamic([]);          // e.g., ["DC01", "Priv-Terminal"]
//
//-----Part 1: Filter and Parse 4768 Events-----
// GPO Path: To capture 4768 events (Kerberos TGT requests), enable on all domain controllers:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Account Logon > Audit Kerberos Authentication Service (Success, Failure)
// Note: 4768 logs every TGT request (success/failure). Critical for tracking both human and machine Kerberos usage and PKINIT (certificate-based) auth.
SecurityEvent
| where TimeGenerated >= ago(14d)
| where EventID == 4768
| where Activity == "4768 - A Kerberos authentication ticket (TGT) was requested."
| extend d = parse_xml(EventData)
| extend EventDataParsed = parse_json(tostring(d.EventData))
| mv-expand DataItem = EventDataParsed.Data
| extend Name = tostring(DataItem["@Name"]), Value = tostring(DataItem["#text"])
//-----Part 2: Summarize/Group by Ticket Request-----
| summarize 
    User = anyif(Value, Name == "TargetUserName"),
    CertIssuerName = anyif(Value, Name == "CertIssuerName"), 
    CertSerialNumber = anyif(Value, Name == "CertSerialNumber"), 
    CertThumbprint = anyif(Value, Name == "CertThumbprint") 
    by TimeGenerated, Activity, Computer
//-----Part 3: Analyst Filtering-----
| where isnotempty(CertIssuerName) and isnotempty(CertSerialNumber) and isnotempty(CertThumbprint)
| where (array_length(TargetUsers) == 0 or User in (TargetUsers))
| where (array_length(TargetCertIssuers) == 0 or CertIssuerName in (TargetCertIssuers))
| where (array_length(TargetDevices) == 0 or Computer in (TargetDevices))
//-----Part 4: Exclude Computer Accounts by Default-----
// (Comment out to include machine TGT requests as well.)
| where User !contains "$"
//-----Part 5: Add IsTargetedUser Flag (ALWAYS true or false)-----
| extend IsTargetedUser = (array_length(TargetUsers) > 0 and User in (TargetUsers))
//-----Part 6: Final Output-----
| project TimeGenerated, IsTargetedUser, User, ['Target Device'] = Computer, CertIssuerName, CertSerialNumber, CertThumbprint, Activity
| sort by TimeGenerated desc
| take 50
