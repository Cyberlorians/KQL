// Objective: Tracks RDP session reconnections (Event ID 4778) in Windows SecurityEvent logs, enabling targeted filtering by user name and reporting on external vs. internal reconnects.
//
// ----- Part 0: Analyst-Driven Targeting (Optional) -----
// To see all activity, leave the list empty: dynamic([])
// To target specific users, add their user names inside the brackets.
let Targeted_User_Names = dynamic([]); // Example: dynamic(["r.finn", "xadmin"])
// 
// ----- Part 1: Filter for Relevant 4778 Events -----
// GPO Path: To capture 4778 events, you must enable the following subcategory:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > Logon/Logoff > Audit Other Logon/Logoff Events (Success)
// Note: This event tracks when a user reconnects to an existing RDP session.
SecurityEvent
| where TimeGenerated > ago(14d)
| where EventID == 4778
| where Activity == "4778 - A session was reconnected to a Window Station."
//
// ----- Part 2: RDP & Custom Filtering -----
// Focus on RDP sessions and exclude common service accounts.
| where SessionName has "RDP"
| where not(AccountName has_any ('svc', 'MSOL', '$'))
//
// ----- Part 3: Flexible Filtering for Targeted Accounts -----
// Apply analyst-driven name filtering if used.
| where array_length(Targeted_User_Names) == 0 or AccountName in (Targeted_User_Names)
//
// ----- Part 4: Analysis & Enrichment -----
// Flags for external reconnections and analyst targeting.
| extend
    IsExternalReconnect = not(ipv4_is_private(ClientAddress)),
    IsTargetedUser = AccountName in (Targeted_User_Names)
//
// ----- Part 5: Final Output -----
// Unique RDP reconnection events, sorted for review.
| distinct TimeGenerated, IsTargetedUser, AccountName, AccountDomain, Computer, SessionName, ClientName, ClientAddress, IsExternalReconnect, Activity
| sort by TimeGenerated desc
| take 50
