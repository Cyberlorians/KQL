// Objective: Detects modifications to Active Directory objects (SecurityEvent, EventID 5136).
// Surfaces who changed what (user/group/computer attributes) to track privileged object and account changes.
//
//-----Part 0: Dynamic Targeting-----
// To target specific users, object classes, or domains, fill in the lists below; leave empty for all.
let TargetUserNames = dynamic([]);      // e.g., ["dadmin", "svc_account"]
let TargetObjectClasses = dynamic([]);  // e.g., ["computer", "user", "group"]
let TargetDomains = dynamic([]);        // e.g., ["CONTOSO"]
//
//-----Part 1: Filter for Directory Object Modification Events-----
SecurityEvent
// GPO Path: To capture 5136 events, enable the following subcategory:
// - Computer Configuration > Policies > Windows Settings > Security Settings > Advanced Audit Policy Configuration > DS Access > Audit Directory Service Changes (Success)
// Note: This event is generated whenever an Active Directory object is modified. It records who made the change, what object, the object class, and which attributes were changed.
| where TimeGenerated > ago(30d)
| where EventID == 5136
| where Activity == "5136 - A directory service object was modified."
//
//-----Part 2: Parse and Expand EventData-----
// Safely parse XML and extract Name/Value pairs for all EventData fields.
| extend d = parse_xml(EventData)
| extend EventDataParsed = parse_json(tostring(d.EventData))
| mv-expand DataItem = EventDataParsed.Data
| extend Name = tostring(DataItem["@Name"]), Value = tostring(DataItem["#text"])
//
//-----Part 3: Summarize to One Row per Object Modification-----
// Collate initiator, DN, class, and attributes per event.
| summarize 
    InitiatedBy = anyif(Value, Name == "SubjectUserName"),
    TargetUserName = anyif(Value, Name == "ObjectDN"),
    ['Object Class'] = anyif(Value, Name == "ObjectClass"),
    ['Attributes'] = make_list_if(Value, Name contains "Attribute")
    by TimeGenerated, Activity
//
//-----Part 4: Analyst Filtering-----
// Return only results matching dynamic lists; leave lists empty for all.
| where (array_length(TargetUserNames) == 0 or InitiatedBy in (TargetUserNames))
| where (array_length(TargetObjectClasses) == 0 or ['Object Class'] in (TargetObjectClasses))
| where (array_length(TargetDomains) == 0 or TargetUserName has_any (TargetDomains))
//
//-----Part 5: Final Output-----
| project TimeGenerated, Activity, InitiatedBy, TargetUserName, ['Object Class'], Attributes = tostring(['Attributes'])
| sort by TimeGenerated desc
| take 100
